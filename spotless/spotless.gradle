apply plugin: 'com.diffplug.spotless'
def ktlintUserData = ['indent_size': '2', 'continuation_indent_size': '2', 'disabled_rules': 'no-wildcard-imports']
def ktlintVersion = "0.38.0"
String[] copiedKotlinFiles = [
        // Add any copied files that should have their copyright header preserved
        "copyright",
].collect { "**/*${it}.kt" } as String[]
String[] copiedJavaFiles = [
        "SerCompat",
        "Suppliers",
        "TzdbZoneDataProvider",
        "copyright",
].collect { "**/*${it}.java" } as String[]
spotless {
    format 'misc', {
        target '**/*.md', '**/.gitignore'
        targetExclude "**/.gradle/**"
        indentWithTabs()
        trimTrailingWhitespace()
        endWithNewline()
    }
    kotlin {
        target "**/*.kt", "**/*.kt"
        targetExclude(copiedKotlinFiles)
        ktlint(ktlintVersion).userData(ktlintUserData)
        licenseHeaderFile rootProject.file('spotless/copyright.kt')
        trimTrailingWhitespace()
        endWithNewline()
    }
    kotlinGradle {
        target '*.gradle.kts'
        ktlint(ktlintVersion).userData(ktlintUserData)
        trimTrailingWhitespace()
        endWithNewline()
    }
    java {
        target "**/src/**/*.java"
        targetExclude '**/spotless/*.java'
        targetExclude(copiedJavaFiles)
        googleJavaFormat("1.8")
        licenseHeaderFile rootProject.file('spotless/copyright.java')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target '**/*.gradle'
        trimTrailingWhitespace()
        endWithNewline()
    }
}
